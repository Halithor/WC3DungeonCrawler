package Dungeon
import Map
import Grid
import MapConstants
import MapGen
import DungeonCreeps
import Execute

// Track the most recent dungeon for the enter trigger.
Dungeon lastDungeon

/** 
  * Class that represents the current dungeon. Has controls for generating and cleaning the 
  * dungeon 
  */
public class Dungeon 
  Map map
  Grid grid
  DungeonCreeps creeps

  construct()
    lastDungeon = this
    Log.debug("Creating map...")
    map = new Map(coord(5,5))
    Log.debug("Generating map...")
    let generator = new MapGenerator(map)
    execute(() -> generator.generateMap())
    destroy generator
    Log.debug("Applying to grid...")
    grid = new Grid()
    grid.applyMapToGrid(map)
    Log.debug("Spawning creeps...")
    creeps = new DungeonCreeps(map, grid)
    creeps.start()

  ondestroy
    destroy map
    destroy grid
    destroy creeps

  // Move the unit into the dungeon.
  function enterUnit(unit u)
    let startingCell = grid.getGridCell(map.start)
    u.setPos(startingCell.center)
    if u.isType(UNIT_TYPE_HERO)
      print(u.getProperName() + " the " + u.getName() + " enters the dungeon.", 10)

init
  // Register a global enter trigger since there's no closure support.
  CreateTrigger()
  ..registerEnterRegion(CreateRegion()..addRect(gg_rct_DungeonEntrance), null)
  ..addAction() ->
    if lastDungeon != null
      lastDungeon.enterUnit(EventData.getEnteringUnit())