package ItemSystem

import ItemIndexer
import ItemGlobals
import AttributeList
import AttributeSystem_Globals
import ItemRandomization
import ItemInfo

/** System to use for all item create/delete actions */
public class ItemSystem
  static function createItem(int itemId, vec2 pos) returns item
    let ret = CreateItem(itemId, pos.x, pos.y)
    // Calling this will create it
    ret.toItemIndex()
    return ret

  static function removeItem(item whichItem)
    whichItem.deindex()
    RemoveItem(whichItem)

  static function createRandomEquipment(int level, vec2 pos) returns item
    let ret = ItemSystem.createItem(kIID_BasicNoEffect, pos)
    let attrs = new AttributeList()
      ..addAttr(ATR_AGILITY_FLAT, GetRandomInt(1, 5))
      ..addAttr(ATR_STRENGTH_FLAT, GetRandomInt(1, 5))
      ..addAttr(ATR_INTELLIGENCE_FLAT, GetRandomInt(1, 5))
    let eType = getRandomEquipmentType()
    let info = new EquipmentInfo("Random " + eType.toString(), getRandomIconForEquipment(eType), 
        level, getRandomRarity(), eType, attrs)
    info.applyToItem(ret)
    return ret
